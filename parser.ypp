%{
	#include "attributes.hpp"
	#include <iostream>
	#include <stdlib.h>

	using namespace std;
    extern int yylineno;
	extern int yylex();		// do they both need to be extern?
	extern CodeBuffer buffer;
	extern scopes* scopesList;
	extern vector<int> goToMainBackPatch;
    void yyerror(const char * message);

    #define TRY_AND_CATCH(statement_to_try)                             \
        try{                                                            \
            (statement_to_try);                                         \
        }catch(parsingExceptions& e){                                   \
             throw parsingExceptions(e.err_type, yylineno, e.id);       \
        }

    #define PRINT_HW3_STUFF 0
    #define PRINT_HW5_STUFF 1

    #define EMIT_TEXT(string) buffer.emit(string)
    #define EMIT_DATA(string) buffer.emitData(string)
    #define EXC static_cast<parsedExp*>
    #define STC static_cast<parsedStatement*>

    #define CODE_COMMENT(str) if(0) buffer.emit(static_cast<string>("# ") + static_cast<string>(str))
%}

%token B
%token BYTE BOOL INT VOID TRUE FALSE RETURN WHILE BREAK SC COMMA ID NUM STRING
%right IF
%right ASSIGN
%left OR
%left AND
%left EQ_OP
%nonassoc RELOP
%left PLUS_MINUS
%left MUL_DIV
%right NOT
%left LPAREN
%left RPAREN
%nonassoc ELSE
%left LBRACE LBRACK RBRACE RBRACK

%%

Program	:	Funcs
    	;

Funcs :
		|	Function Funcs
		;
Function : FunctionDefinition LBRACE Statements RBRACE_t {
                                                                    if($1->single_var.name == "main"){
                                                                        buffer.bpatch(goToMainBackPatch,scopesList->functions.front().beginLabel);
                                                                    }

                                                                    codeGenerator::returnVoidFunction();
                                                                    buffer.emit(string("# End of function"));

                                                                    delete $1; delete $3;}
        ;

FunctionDefinition : RetType ID LPAREN Formals Gen_Label RPAREN {TRY_AND_CATCH(scopesList->addFunction(*$1,*$2,*$4,*EXC$5));
                                                                   TRY_AND_CATCH(scopesList->newFunctionScope(*$4));
                                                                   $$ = $2;
                                                                   buffer.emit(string("# Function: ") + $2->single_var.name);

                                                                   delete $1; delete $4; delete $5;}
                   ;

RetType : Type          {$$ = $1;}
        | VOID          {$$ = new parsedData(Type(Type::VOID));}
        ;

Formals	:	                    {$$ = new parsedData();}  //Lior
		|	FormalsList         {$$ = $1;}
		;

Type_ID : Type ID       {if(scopesList->containsIdName($2->getName()))
                            throw parsingExceptions(parsingExceptions::ERR_DEF, yylineno, $2->getName());

                         $$ = new parsedData($1->single_var.type, $2->single_var.name);
                         delete $1; delete $2;
                        }    //Lior
		;

FormalsList :   FormalDecl {$$ = new parsedData(*$1, IS_CALL);
                            delete $1;
                           }
            |   FormalDecl COMMA FormalsList {$$ = new parsedData(*$1,*$3) ;
                                              delete $1; delete $3;
                                             }
            ;

FormalDecl  :   Type_ID                     {TRY_AND_CATCH($$ = new parsedData(*$1));
                                             delete $1;
                                            }
            |   Type_ID LBRACK NUM_T RBRACK {TRY_AND_CATCH($$ = new parsedData(*$1, *$3, IS_ARRAY));
                                             delete $1; delete $3;
                                            }
            ;

NUM_T :	NUM NUM_T2  {try{$$ = new parsedData($2->single_var.type, $1->single_var.value);delete $1; delete $2;}
                     catch(parsingExceptions& e){throw parsingExceptions(e.err_type, yylineno, e.id);}}
		;

NUM_T2 :                {$$ = new parsedData(Type(Type::INTEGER));}     //Lior
       | B              {$$ = new parsedData(Type(Type::BYTE));}    //Lior
       ;


Statements  :   Statement {$$ = $1;}
            |   Statement Statements {
                                      $$ = new parsedStatement();
                                      STC$$->breakList = mergeVectors(STC$1->breakList,STC$2->breakList);
                                      delete $1; delete $2;
                                     }
            ;

Statement : 	LBRACE_t Statements RBRACE_t {$$ = $2;}
			|	Type_ID SC          {TRY_AND_CATCH(scopesList->addIdNotArray(*$1));
                                     $$ = new parsedStatement();

                                     codeGenerator::generateNewSingleVarCreation(*$1);
			                         delete $1;
			                         }
			|	Type_ID ASSIGN Exp SC           {TRY_AND_CATCH(scopesList->addIdNotArray(*$1));
			                                     TRY_AND_CATCH(scopesList->verifyAssign(*$1,*$3));
			                                     $$ = new parsedStatement();
                                                regClass tempReg = regAlloc();

                                                 if($1->isBool()){
                                                    codeGenerator::assignBoolIntoV1(*EXC$3);
                                                    codeGenerator::generateNewSingleVarCreation(*$1);
                                                    buffer.emit(string("sw $v1, ($sp)"));
                                                 }
                                                 else{
			                                        codeGenerator::generateNewSingleVarCreation(*$1);
			                                        codeGenerator::assignValueToId(scopesList->getId($1->getName()),*EXC$3);
                                                 }

                                                 if(!EXC$3->isBoolExp()) regFree(EXC$3->reg);

						                          regFree(tempReg);
						                          delete $1; delete $3;
						                         }
			|	Type_ID LBRACK NUM_T RBRACK SC      {TRY_AND_CATCH(scopesList->addIdArray(*$1,*$1,*$3));
			                                         $$ = new parsedStatement();

                                                     string s1 = "subu $sp, $sp, ";
                                                     s1 += num_to_string(scopesList->getId($1->getName()).type.size()*4);
                                                     EMIT_TEXT(s1);

                                                     regClass valueToInitReg = regAlloc();
                                                     s1 = string("add ") + valueToInitReg.toString() + string(", $0, ");
                                                     if($1->single_var.type.kind == Type::BOOL)
                                                        s1 += codeGenerator::falseValueRepresentation();
                                                     else
                                                        s1 += string("$0");
                                                     EMIT_TEXT(s1);

                                                     for(int i=0; i < $3->single_var.value ; i++){
                                                        regClass tempReg = regAlloc();

                                                        EMIT_TEXT(string("addu ") + tempReg.toString() + string(", $sp, ") + num_to_string(i*4));

                                                        string s2 = string("sw ") + valueToInitReg.toString();
                                                        s2 += string(", (");
                                                        s2 += tempReg.toString();
                                                        s2 += string(")");
                                                        EMIT_TEXT(s2);

                                                        regFree(tempReg);
                                                    }

                                                     regFree(valueToInitReg);
			                                         delete $1; delete $3;
			                                        }
			|	ID ASSIGN Exp SC        {if (!scopesList->containsIdName($1->getName()))
			                                throw parsingExceptions(parsingExceptions::ERR_UNDEF,yylineno, $1->getName() );
                                         TRY_AND_CATCH(scopesList->verifyAssign(*$1,*$3));

                                         $$ = new parsedStatement();
                                         codeGenerator::assignValueToId(scopesList->getId($1->getName()),*EXC$3);

                                         if(!EXC$3->isBoolExp()) regFree(EXC$3->reg);
                                         delete $1; delete $3;
                                        }
			|	ID LBRACK Exp RBRACK ASSIGN Exp SC  {if (!scopesList->containsIdName($1->getName()))
			                                            throw parsingExceptions(parsingExceptions::ERR_UNDEF,yylineno, $1->getName());
                                                     TRY_AND_CATCH(scopesList->verifyAssignToArray(*$1,*$3,*$6));

                                                     $$ = new parsedStatement();
                                                     codeGenerator::assignValueToArray(scopesList->getId($1->getName()),*EXC$3,*EXC$6);

                                                     if(!(EXC$6->regType == BPInfo::REG_TYPE_UNDEF))
                                                        regFree(EXC$6->reg);
                                                     regFree(EXC$3->reg);
                                                     delete $1; delete $3; delete $6;
                                                    }
			|	Call SC             {$$ = new parsedStatement();

			                        if(!EXC$1->trueList.empty())
			                            buffer.bpatch(EXC$1->trueList,buffer.genLabel());
			                        if(!EXC$1->falseList.empty())
                                        buffer.bpatch(EXC$1->falseList,buffer.genLabel());

			                        if(EXC$1->regType != BPInfo::REG_TYPE_UNDEF) regFree(EXC$1->reg);
			                        delete $1;
			                        }
			|	RETURN SC           {TRY_AND_CATCH(scopesList->verifyReturnTypeVoid());
			                        $$ = new parsedStatement(); // so delete later will be possible

			                        codeGenerator::returnVoidFunction();

			                        }
			|	RETURN Exp SC       {TRY_AND_CATCH(scopesList->verifyReturnType(*$2));
					                 $$ = new parsedStatement(); // so delete later will be possible

                                    codeGenerator::returnFunction(*EXC$2);

					                 delete $2;
					                }

           	|	While_Statement     {$$ = $1;}
			|	BREAK SC            {TRY_AND_CATCH(scopesList->verifyBreakBlock());
			                         $$ = new parsedStatement();
                                     vector<scope> while_sub_scopes = scopesList->findFirstWhileScope();
                                     while(!while_sub_scopes.empty()){
                                        scopesList->removeScopeVars(while_sub_scopes.back());
                                        while_sub_scopes.pop_back();
			                         }
			                         STC$$->breakList.push_back(EMIT_TEXT("j "));
			                        }

			| If_Begin If_Continue {
                                    $$ = new parsedStatement();

                                    STC$$->breakList = STC$2->breakList;
                                    buffer.bpatch(STC$1->trueList, STC$2->ifTrueLabel);
                                    buffer.bpatch(STC$1->falseList, STC$2->ifFalseLabel);

                                    delete $1; delete $2;
                                   }
			;

If_Begin    :  IF LPAREN Exp {if(!$3->isBool()) throw parsingExceptions(parsingExceptions::ERR_MISMATCH,yylineno);
                              $$ = new parsedStatement(*(STC$3));
                              delete $3;
                             }
            ;

If_Continue :   RPAREN Open_Scope_and_create_label Statement {scopesList->removeScope();

                $$ = new parsedStatement();
                STC$$->breakList = STC$3->breakList;
                STC$$->ifTrueLabel = STC$2->beginLabel;
                STC$$->ifFalseLabel = buffer.genLabel();

                delete $2; delete $3; // second delete might be dangerous
            }
            |   RPAREN Open_Scope_and_create_label Statement ELSE Go_To_End_of_If_Statement CloseScope Open_Scope Gen_Label Statement {scopesList->removeScope();

                $$ = new parsedStatement();

                STC$$->breakList = mergeVectors(STC$3->breakList,STC$9->breakList);

                string endLabel = buffer.genLabel();
                buffer.bpatch(STC$5->nextList,endLabel);
                STC$$->ifTrueLabel = STC$2->beginLabel;
                STC$$->ifFalseLabel = STC$8->beginLabel;

                delete $2; delete $3; delete $5; delete $8; delete $9;
            }
            ;

Go_To_End_of_If_Statement : {
                             $$ = new parsedStatement();
                             STC$$->nextList.push_back(EMIT_TEXT("j "));
                            }

Gen_Label : {$$ = new parsedStatement(buffer.genLabel());}
Open_Scope : { scopesList->newRegularScope(false, false);}
           ;

Open_Scope_and_create_label:{
                             scopesList->newRegularScope(false, false);
                             $$ = new parsedStatement(buffer.genLabel());
                             }
           ;
CloseScope : { scopesList->removeScope(); }
           ;

While_Statement : While_Open Gen_Label Statement {
                                        $$ = new parsedStatement(); // for the delete at higher levels
                                        STC$$->beginLabel = STC$1->beginLabel;
                                        regClass trueValueReg = regAlloc();
                                        buffer.emit(string("add ") + trueValueReg.toString() + string(", ")
                                            + trueValueReg.toString() + string(", ") + codeGenerator::trueValueRepresentation());

                                        buffer.emit(string("beq ") + STC$1->reg.toString() + string(", ")
                                            + trueValueReg.toString() + string(", ") + STC$1->beginLabel);

                                        string endLabel = buffer.genLabel();

                                        buffer.bpatch(STC$3->breakList,endLabel);
                                        //buffer.bpatch(STC$3->breakList,STC$1->endLabel);
                                        //buffer.emit(string("j ")+STC$$->beginLabel);

                                        regFree(trueValueReg);
                                        regFree(STC$1->reg);
                                        scopesList->removeScope();
                                        delete $1; delete $2;
                                       }
            ;

While_Open : WHILE LPAREN WhileExp RPAREN {TRY_AND_CATCH(scopesList->verifyExpIsBool(*$3));
                                      TRY_AND_CATCH(scopesList->newRegularScope(true, true));

                                      $$ = new parsedStatement();
                                      assert(EXC$3->boolType != BPInfo::BOOL_TYPE_UNDEF);
                                      if(EXC$3->boolType == BPInfo::BOOL_TYPE_BRANCH_LISTS){
                                        STC$$->reg = regAlloc();
                                        codeGenerator::assignBoolIntoV1(*EXC$3);
                                        buffer.emit(string("addu ") + STC$$->reg.toString() + string(", $v1, $0"));
                                        }
                                      else{
                                        STC$$->reg = STC$3->reg;
                                      }

                                      STC$$->boolType = BPInfo::BOOL_TYPE_VALUE;
                                      STC$$->regType = BPInfo::REG_TYPE_VALUE;
                                      STC$$->beginLabel = STC$3->beginLabel;
                                      STC$$->endLabel = STC$3->endLabel;

                                      delete $3;}
        ;

WhileExp : Save_Regs Exp_t  {$$ = $1;
                                       // STC$$->beginLabel = STC$1->beginLabel;
                                        STC$$->endLabel = STC$2->beginLabel;
                                        }
         ;
Exp_t       :       Exp {$$ = $1;
                        //STC$$->beginLabel = buffer.genLabel();
                        EXC$$->reg = regAlloc();
                        codeGenerator::assignBoolIntoV1(*EXC$1);
                         callerRestoreRegisters();
                         buffer.emit(string("addu ")+(EXC$$->reg.toString())+string("$v1, $0"));
                        }
            ;
Save_Regs   :       {$$ = new parsedStatement(buffer.genLabel());
                    callerSaveRegisters();}
             ;

Call	:	ID LPAREN ExpList RPAREN    {try {scopesList->verifyFunctionCall(*$1,*$3);}
                                            catch(parsingExceptions& e){
                                            if(e.err_type == parsingExceptions::ERR_PROTOTYPE_MISMATCH)
                                                throw parsingExceptions(e.err_type,
                                                yylineno, $1->getName(),scopes::listToVector(scopesList->getFunction($1->getName()).inputTypes));
                                            else
                                                throw parsingExceptions(e.err_type,
                                                yylineno, $1->getName());
                                            }

                                         $$ = new parsedExp((scopesList->getFunction($1->getName())).return_type);

                                         EXC$$->reg = regAlloc();
                                         if((scopesList->getFunction($1->getName())).return_type.kind == Type::VOID)
                                            EXC$$->regType = BPInfo::REG_TYPE_UNDEF;
                                         else
                                            EXC$$->regType = BPInfo::REG_TYPE_VALUE;

                                         codeGenerator::callFunction(scopesList->getFunction($1->getName()).beginLabel
                                                                      ,*EXC$3,EXC$$->reg);

                                         if(EXC$$->regType == BPInfo::REG_TYPE_UNDEF)
                                            regFree(EXC$$->reg);

                                         regClass tempReg = regAlloc();
                                         if(scopesList->getFunction($1->getName()).return_type.kind == Type::BOOL){
                                            buffer.emit(string("add ") + tempReg.toString() + string(", $0, ") +
                                                codeGenerator::trueValueRepresentation());

                                            EXC$$->trueList.push_back(buffer.emit(string("beq ") + tempReg.toString() + string(", ") +
                                                EXC$$->reg.toString() + string(", ")));
                                            EXC$$->falseList.push_back(buffer.emit(string("j ")));

                                            regFree(EXC$$->reg);
                                            EXC$$->regType = BPInfo::REG_TYPE_UNDEF;
                                            EXC$$->boolType = BPInfo::BOOL_TYPE_BRANCH_LISTS;
                                         }

                                          regFree(tempReg);

                                         delete $1; delete $3;}
		|	ID LPAREN RPAREN            {try {scopesList->verifyNoParametersFunctionCall(*$1);
		                                    }catch(parsingExceptions& e){
		                                        if(e.err_type == parsingExceptions::ERR_PROTOTYPE_MISMATCH)
                                                    throw parsingExceptions(e.err_type,
                                                    yylineno, $1->getName(), scopes::listToVector(scopesList->getFunction($1->getName()).inputTypes));
                                                else throw parsingExceptions(e.err_type,yylineno, $1->getName());
                                        }
		                                 $$ = new parsedExp((scopesList->getFunction($1->getName())).return_type);

                                         EXC$$->reg = regAlloc();
                                         if((scopesList->getFunction($1->getName())).return_type.kind == Type::VOID)
                                            EXC$$->regType = BPInfo::REG_TYPE_UNDEF;
                                         else
                                            EXC$$->regType = BPInfo::REG_TYPE_VALUE;

                                         parsedExp dummy(Type::UNDEF);
                                         codeGenerator::callFunction(scopesList->getFunction($1->getName()).beginLabel,dummy,EXC$$->reg);

                                         if(EXC$$->regType == BPInfo::REG_TYPE_UNDEF)
                                            regFree(EXC$$->reg);

                                         regClass tempReg = regAlloc();
                                         if(scopesList->getFunction($1->getName()).return_type.kind == Type::BOOL){
                                            buffer.emit(string("add ") + tempReg.toString() + string(", $0, ") +
                                                codeGenerator::trueValueRepresentation());

                                            EXC$$->trueList.push_back(buffer.emit(string("beq ") + tempReg.toString() + string(", ") +
                                                EXC$$->reg.toString() + string(", "))) ;
                                            EXC$$->falseList.push_back(buffer.emit(string("j ")));

                                            regFree(EXC$$->reg);
                                            EXC$$->regType = BPInfo::REG_TYPE_UNDEF;
                                            EXC$$->boolType = BPInfo::BOOL_TYPE_BRANCH_LISTS;
                                         }

                                         regFree(tempReg);
		                                 delete $1;}
		;

ExpList:    Exp ExpList2 {  $$ = new parsedExp(*EXC$1,*EXC$2) ;
                            $$->list_of_vars.front().reg = EXC$1->reg;
                            $$->list_of_vars.front().regType = EXC$1->regType;

                            if(EXC$1->isBool()){
                                assert(EXC$1->boolType == BPInfo::BOOL_TYPE_BRANCH_LISTS && "if got here, it might be due use of bool as not branch list");
                                $$->list_of_vars.front().reg = regAlloc();
                                codeGenerator::assignBoolIntoV1(*EXC$1);
                                buffer.emit(string("addu ") + $$->list_of_vars.front().reg.toString() + string(", $v1, $0"));
                                $$->list_of_vars.front().boolType = BPInfo::BOOL_TYPE_VALUE;
                                $$->list_of_vars.front().regType = BPInfo::REG_TYPE_VALUE;
                            }

		                    delete $1; delete $2;}

ExpList2 :                  { $$ = new parsedExp(IS_CALL); }
         | COMMA ExpList    { $$ = $2;}
         ;

Type	:	INT              {$$ = new parsedData(Type(Type::INTEGER));}
		|	BYTE             {$$ = new parsedData(Type(Type::BYTE));}
		| 	BOOL             {$$ = new parsedData(Type(Type::BOOL));}
		;

Exp		:	LPAREN Exp RPAREN       {$$ = $2;}
		| 	ID LBRACK Exp RBRACK    {if (!scopesList->containsIdName($1->getName()))  //ID doesn't exist
                                        throw parsingExceptions(parsingExceptions::ERR_UNDEF,yylineno, $1->getName());
                                     if(!(scopesList->getId($1->getName()).type.kind == Type::ARRAY)) //ID is not an array
                                        throw parsingExceptions(parsingExceptions::ERR_MISMATCH,yylineno);
                                     if (!($3->isInteger())) // Exp is not a numeric type
                                        throw parsingExceptions(parsingExceptions::ERR_INVALID_ARRAY_SIZE, yylineno, $1->getName());

                                     $$ = new parsedExp(scopesList->getId($1->getName()).type.arrayType);

                                     EXC$$->reg = regAlloc();
                                     EXC$$->regType = BPInfo::REG_TYPE_VALUE;

                                     regClass addressReg = regAlloc();

                                     codeGenerator::generateArrayOverflowCheck(scopesList->getId($1->getName()).type.arrayLength,EXC$3->reg);
                                     codeGenerator::generateArrayLocationCalc(addressReg,EXC$3->reg,codeGenerator::idOffsetFromFp(scopesList->getId($1->getName())));

                                     buffer.emit(string("lw ") + EXC$$->reg.toString() + string(", (") + addressReg.toString() + string(")"));

                                     // case that id is boolean, then creates true/false lists for use of upstream code
                                     if(scopesList->getId($1->getName()).type.arrayType == Type::BOOL){
                                        regClass boolTrueValue = regAlloc();
                                        string s1 = string("add ") + boolTrueValue.toString() + string(", $0, ");
                                        s1 += codeGenerator::trueValueRepresentation();
                                        EMIT_TEXT(s1);

                                        s1 = string("beq ") + EXC$$->reg.toString() + string(", ") + boolTrueValue.toString() + string(", ");
                                        EXC$$->trueList.push_back(buffer.emit(s1));
                                        EXC$$->falseList.push_back(buffer.emit(string("j ")));

                                        regFree(EXC$$->reg);
                                        EXC$$->regType = BPInfo::REG_TYPE_UNDEF; // hope this is enough for representing null
                                        EXC$$->boolType = BPInfo::BOOL_TYPE_BRANCH_LISTS;
                                        regFree(boolTrueValue);
                                     }

                                     regFree(addressReg);
                                     regFree(EXC$3->reg);
                                     delete $1; delete $3;
		                            }
		| 	Exp PLUS_MINUS Exp      {TRY_AND_CATCH($$ = new parsedExp(*$1,*$3, MATH_OP));
		                             EXC$$->reg = regAlloc();
                                     EXC$$->regType = BPInfo::REG_TYPE_VALUE;

                                     string s1 = codeGenerator::arithmeticOpToString($2->pd_op);
                                     buffer.emit(s1 + EXC$$->reg.toString() + ", " + EXC$1->reg.toString() + ", " + EXC$3->reg.toString());

                                     if($$->single_var.type.kind == Type::BYTE){
                                        codeGenerator::byteArithmeticMasking(EXC$$->reg);
                                     }

                                     regFree(EXC$1->reg);
                                     regFree(EXC$3->reg);
                                     delete $1; delete $3;
                                     }
		|	Exp MUL_DIV Exp         {TRY_AND_CATCH($$ = new parsedExp(*$1,*$3, MATH_OP));
		                             EXC$$->reg = regAlloc();
		                             EXC$$->regType = BPInfo::REG_TYPE_VALUE;

		                             string s1 = codeGenerator::arithmeticOpToString($2->pd_op);

		                             if(s1 == "div ")
		                                EMIT_TEXT(codeGenerator::divisionByZeroCheck(EXC$3->reg));

		                            buffer.emit(s1 + EXC$$->reg.toString() + ", " + EXC$1->reg.toString() + ", " + EXC$3->reg.toString());

                                     if($$->single_var.type.kind == Type::BYTE)
                                        codeGenerator::byteArithmeticMasking(EXC$$->reg);

                                     regFree(EXC$1->reg);
                                     regFree(EXC$3->reg);
                                     delete $1; delete $3;
                                     }
		| 	ID                      {if (!scopesList->containsIdName($1->getName()))
		                                throw parsingExceptions(parsingExceptions::ERR_UNDEF,yylineno, $1->getName());

		                             $$ = new parsedExp(scopesList->getId($1->getName()).type);
		                             EXC$$->reg = regAlloc();
                                     regClass addressReg = regAlloc();

                                     codeGenerator::putIdAddressInRegister(scopesList->getId($1->getName()),addressReg);

                                     if($$->kind == parsedData::DK_ARRAY){
                                        EXC$$->regType = BPInfo::REG_TYPE_REFERENCE;
                                        buffer.emit(string("addu ") + EXC$$->reg.toString() + string(", $0, ") + addressReg.toString());
                                     }
                                     else{
                                        EXC$$->regType = BPInfo::REG_TYPE_VALUE;
                                        buffer.emit(string("lw ") + EXC$$->reg.toString() + string(", (") + addressReg.toString() + string(")"));
                                     }

                                     // case that id is boolean, then creates true/false lists for use of upstream code
                                     if(scopesList->getId($1->getName()).type.kind == Type::BOOL){
                                        regClass boolTrueValue = regAlloc();
                                        string s1 = string("add ") + boolTrueValue.toString() + string(", $0, ");
                                        s1 += codeGenerator::trueValueRepresentation();
                                        EMIT_TEXT(s1);

                                        s1 = string("beq ") + EXC$$->reg.toString() + string(", ") + boolTrueValue.toString() + string(", ");
                                        EXC$$->trueList.push_back(buffer.emit(s1));
                                        EXC$$->falseList.push_back(buffer.emit(string("j ")));

                                        regFree(EXC$$->reg);
                                        EXC$$->regType = BPInfo::REG_TYPE_UNDEF; // hope this is enough for representing null
                                        EXC$$->boolType = BPInfo::BOOL_TYPE_BRANCH_LISTS;
                                        regFree(boolTrueValue);
                                     }

                                     regFree(addressReg);
                                     delete $1;
		                            }

		|	Call                    {$$ = $1;}

        |	NUM_T	                {$$ = new parsedExp(*$1);
                                    EXC$$->reg =  regAlloc();
                                    EXC$$->regType = BPInfo::REG_TYPE_VALUE;
                                    string s1 = "li "; // load immediate
                                    string s2 = EXC$$->reg.toString();
                                    string s3 = ", ";
                                    string s4 = num_to_string($$->single_var.value);
                                    EMIT_TEXT(s1 + s2 + s3 + s4);
                                    }

        |	STRING                  {$$ = new parsedExp(Type(Type::STRING));    // return string type
                                     EXC$$->reg =  regAlloc();
                                     EXC$$->regType = BPInfo::REG_TYPE_REFERENCE;

                                     // create string in .data segment
                                     string stringLocation = codeGenerator::pushStringToDataBuffer(*$1);

                                     // generate code " la  $$->reg, <string location label>"
                                     string s1 = "la "; // load address, need to verify it works with labels
                                     string s2 = EXC$$->reg.toString();
                                     string s3 = ", ";
                                     string s4 = stringLocation;
                                     EMIT_TEXT(s1 + s2 + s3 + s4);
                                    }
        |	TRUE	                {$$ = new parsedExp(Type(Type::BOOL)); // return bool type
                                     EXC$$->boolType = BPInfo::BOOL_TYPE_BRANCH_LISTS;
                                     EXC$$->regType = BPInfo::REG_TYPE_UNDEF;

                                     EXC$$->trueList.push_back(EMIT_TEXT("j "));
                                    }

		|	FALSE	                {$$ = new parsedExp(Type(Type::BOOL)); // return bool type
		                             EXC$$->boolType = BPInfo::BOOL_TYPE_BRANCH_LISTS;
                                     EXC$$->regType = BPInfo::REG_TYPE_UNDEF;
		                             EXC$$->falseList.push_back(EMIT_TEXT("j "));
                                    }

		|	NOT Exp	                {TRY_AND_CATCH($$ = new parsedExp(*$2, BOOL_OP));
		                             assert(EXC$2->boolType != BPInfo::BOOL_TYPE_UNDEF);
		 		                     if(EXC$2->boolType == BPInfo::BOOL_TYPE_BRANCH_LISTS){
		 		                        EXC$$->trueList = EXC$2->falseList;
		 		                        EXC$$->falseList = EXC$2->trueList;
		 		                     }
		 		                     else
		 		                        assert("it is ok, need to implement" && 0);
		 		                     EXC$$->boolType = EXC$2->boolType;
		 		                     delete $2;
		 		                    }
		|	Exp OR Gen_Label Exp              {TRY_AND_CATCH($$ = new parsedExp(*$1,*$4, BOOL_OP));
                                        if(PRINT_DEBUG)
        		                            cout << "parsed exp OR exp: "
        		                            << $1->getName() << " " << $3->getName()
        		                            << endl;

                                     assert(EXC$1->boolType != BPInfo::BOOL_TYPE_UNDEF);
                                     assert(EXC$4->boolType != BPInfo::BOOL_TYPE_UNDEF);


                                     buffer.bpatch(EXC$1->falseList,EXC$3->beginLabel);
                                     EXC$$->falseList = EXC$4->falseList;
                                     EXC$$->trueList = mergeVectors(EXC$4->trueList,EXC$1->trueList);

                                     EXC$$->boolType = BPInfo::BOOL_TYPE_BRANCH_LISTS;

        		                     delete $1; delete $3;
                                    }  // make sure Exp and Exp are of type bool
		|	Exp AND Gen_Label Exp	            {TRY_AND_CATCH($$ = new parsedExp(*$1,*$4, BOOL_OP));
                                        if(PRINT_DEBUG)
        		                            cout << "parsed exp AND exp: "
        		                            << $1->getName() << " " << $3->getName()
        		                            << endl;

                                     assert(EXC$1->boolType != BPInfo::BOOL_TYPE_UNDEF);
                                     assert(EXC$4->boolType != BPInfo::BOOL_TYPE_UNDEF);

        		                     buffer.bpatch(EXC$1->trueList,EXC$3->beginLabel);
                                     EXC$$->trueList = EXC$4->trueList;
                                     EXC$$->falseList = mergeVectors(EXC$4->falseList,EXC$1->falseList);

                                    EXC$$->boolType = BPInfo::BOOL_TYPE_BRANCH_LISTS;

        		                     delete $1; delete $3;
                                    }     // make sure Exp and Exp are of type bool
		|	Exp EQ_OP Exp           {TRY_AND_CATCH($$ = new parsedExp(*$1,*$3, REL_OP));
		                                if(PRINT_DEBUG)
		                                    cout << "parsed exp EQ_OP exp: "
		                                     << $1->getName() << " " << $3->getName()
		                                     << endl;

                                     string s1 = codeGenerator::opToBranchString($2->pd_op); // branch type
                                     string s2 = EXC$1->reg.toString();
                                     string s3 = ", ";
                                     string s4 = EXC$3->reg.toString();
                                     string s5 = ", ";
                                     EXC$$->trueList.push_back(EMIT_TEXT(s1 + s2 + s3 + s4 + s5));
                                     EXC$$->falseList.push_back(EMIT_TEXT("j "));
                                     EXC$$->boolType = BPInfo::BOOL_TYPE_BRANCH_LISTS;
                                     EXC$$->regType = BPInfo::REG_TYPE_UNDEF;

                                     regFree(EXC$1->reg);
                                     regFree(EXC$3->reg);
                                     delete $1; delete $3;
                                     }
		|	Exp RELOP Exp           {TRY_AND_CATCH($$ = new parsedExp(*$1,*$3, REL_OP));
		                                if(PRINT_DEBUG)
        		                             cout << "parsed exp RELOP exp: "
        		                             << $1->getName() << " " << $3->getName()
        		                             << endl;

		                             string s1 = codeGenerator::opToBranchString($2->pd_op); // branch type
                                     string s2 = EXC$1->reg.toString();
                                     string s3 = ", ";
                                     string s4 = EXC$3->reg.toString();
                                     string s5 = ", ";
                                     EXC$$->trueList.push_back(EMIT_TEXT(s1 + s2 + s3 + s4 + s5));
                                     EXC$$->falseList.push_back(EMIT_TEXT("j "));
                                     EXC$$->boolType = BPInfo::BOOL_TYPE_BRANCH_LISTS;
                                     EXC$$->regType = BPInfo::REG_TYPE_UNDEF;

                                     regFree(EXC$1->reg);
                                     regFree(EXC$3->reg);
		                             delete $1; delete $3;
		                            }
		;


LBRACE_t : LBRACE { scopesList->newRegularScope(false, false); }
         ;

RBRACE_t : RBRACE{ scopesList->removeScope(); }
         ;

%%

int main()
{
    // magic from stack overflow :) redirect cout somewhere so we can keep old printing code as is

    streambuf* oldCoutStreamBuf;
    ostringstream strCout;
    if(!PRINT_HW3_STUFF){
        oldCoutStreamBuf = cout.rdbuf();
        cout.rdbuf( strCout.rdbuf() );
    }
    bool print_hw3_error = false;
	// initSystem
	scopesList = new scopes();
	scopesList->scopesList.push_front(scope(0,false, false));
    scopesList->addInitialFunction( Type(Type::VOID),string("print"), Type(Type::STRING));
    scopesList->addInitialFunction( Type(Type::VOID),string("printi"), Type(Type::INTEGER));

    codeGenerator::initiateHW5();

    try{
	    yyparse();
	}catch(parsingExceptions& e){
	    scopesList->need_to_print = false;
	    print_hw3_error = true;
	}
	catch(...){
	    scopesList->need_to_print = false;
 	    print_hw3_error = true;
       if(PRINT_DEBUG) cout << "Something wierd happened!!!" << endl;
	}
	if(scopesList->need_to_print){      //check valid main function in the scopelist
	    if(!scopesList->containsFunctionName("main")){
   	        scopesList->need_to_print = false;
 	        print_hw3_error = true;
           errorMainMissing();
    	}else{
    	    function main_func = scopesList->getFunction("main");
    	    if((main_func.return_type.kind != main_func.return_type.VOID ||
    	            !main_func.inputTypes.empty())){
                scopesList->need_to_print = false;
                errorMainMissing();
	            print_hw3_error = true;
                if(PRINT_DEBUG) cout <<"in the if to check the main function" << endl;
            }
        }
    }

    delete scopesList;

    // Restore old cout.
    if(!PRINT_HW3_STUFF || print_hw3_error) cout.rdbuf( oldCoutStreamBuf );
    if(PRINT_HW5_STUFF && scopesList->need_to_print){
	    buffer.printCodeBuffer();
	    buffer.printDataBuffer();
	 }
}

void yyerror(const char * message)
{
    throw parsingExceptions(parsingExceptions::ERR_SYN, yylineno);
}